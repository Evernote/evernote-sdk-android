apply plugin: 'android-library'

group = GROUP
version = VERSION_NAME

description = "Android-Evernote SDK"

android {
    compileSdkVersion 17
    buildToolsVersion "19.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile group: 'org.scribe', name: 'scribe', version:'1.3.1'
    compile group: 'com.evernote', name: 'evernote-api', version:'1.25'
}

task install(type: Exec, dependsOn: assemble) {
  // TODO get aar path dynamically when i'm not being lazy
  executable = 'mvn'
  args = [
      "install:install-file",
      "-DgroupId=${GROUP}",
      "-DartifactId=${POM_ARTIFACT_ID}",
      "-Dversion=${version}",
      "-Dpackaging=${POM_PACKAGING}",
      "-Dfile=build/outputs/aar/library-${version}.aar"
  ]
}

apply from: 'https://raw.github.com/shamanland/gradle-mvn-push/master/gradle-mvn-push.gradle'
